buildscript {
	ext {
		grailsVersion = project.grailsVersion
	}
	repositories {
		mavenLocal()
		maven { url "https://repo.grails.org/grails/core" }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
	}
}

plugins {
	id "io.spring.dependency-management" version "0.5.2.RELEASE"
	id "com.jfrog.bintray" version "1.2"
}

version "2.0.3"
group "org.grails.plugins"

apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
// Used for publishing to central repository, remove if not needed
apply from: 'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/grailsCentralPublishing.gradle'
apply from: 'https://raw.githubusercontent.com/grails/grails-profile-repository/master/profiles/plugin/templates/bintrayPublishing.gradle'

ext {
	grailsVersion = project.grailsVersion
	gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
	imports {
		mavenBom "org.grails:grails-bom:$grailsVersion"
	}
	applyMavenExclusions false
}

dependencies {
	provided 'org.springframework.boot:spring-boot-starter-logging'
	provided "org.springframework.boot:spring-boot-starter-actuator"
	provided "org.springframework.boot:spring-boot-autoconfigure"
	provided "org.springframework.boot:spring-boot-starter-tomcat"

	provided "org.grails:grails-web-boot"
	provided "org.grails:grails-dependencies"
	provided 'javax.servlet:javax.servlet-api:3.1.0'

	testCompile "org.grails:grails-plugin-testing"
	testCompile "org.grails.plugins:hibernate"
	testCompile 'junit:junit:4.13.2'

	console "org.grails:grails-console"
	compile 'net.java.dev.jets3t:jets3t:0.9.4'
	compile 'org.apache.httpcomponents:httpclient:4.3.4'
	// httpcore 4.3.2 is a dependency of httpclient 4.3.4
	compile 'org.apache.httpcomponents:httpcore:4.4.16'
	compile 'javax.mail:mail:1.4.1'
	compile 'commons-logging:commons-logging:1.1.1'
	compile 'org.codehaus.jackson:jackson-core-asl:1.7.2'

	compile('com.amazonaws:aws-java-sdk:1.9.13')
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleWrapperVersion
}

bintray {
	user = System.getenv("BINTRAY_USER") ?: project.hasProperty("bintrayUser") ? project.bintrayUser : ''
	key = System.getenv("BINTRAY_KEY") ?: project.hasProperty("bintrayKey") ? project.bintrayKey : ''
	publications = ['maven']
	pkg {
		repo = 'grails-plugins'
		userOrg = 'grails-aws'
		name = "org.grails.plugins:grails-aws"
		desc = "Grails AWS Plugin"
		websiteUrl = "http://grails.org/plugin/grails-aws"
		issueTrackerUrl = "https://github.com/grails-aws/grails-aws/issues"
		vcsUrl = "https://github.com/grails-aws/grails-aws"
		licenses = ['Apache-2.0']
		publicDownloadNumbers = true
	}
}

jar {
	exclude 'test/aws/**',
			"grails-app/views/**",
			"grails-app/controllers/**/*Controller.groovy",
			"grails-app/services/**/*Service.groovy",
			"src/docs/**",
			"testScripts/*.groovy"
}

bintrayUpload.dependsOn sourcesJar, javadocJar
